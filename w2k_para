#!/bin/tcsh
#Convert MPI machines file to the WIEN2k .machines file
#MPI is disabled (only k-points parallel used)
#This is part of qw2k.sh and related scripts
set FTMP = ".machinestmp"
#set FMACH = "$FTMPDIR/machines"
set FMACH = $1
set FWMACH = ".machines"

echo "Original MPI machines file:"
cat $FMACH

echo "" > ${FTMP}
rm -rf ${FTMP}
echo "Read the list of processes..."
foreach mach ("`cat $FMACH`")
    echo $mach
    set split = ($mach)
    if ($#split == 2) then #SGE type
        set procs = "$split[2]:gas/slots=//"
        set i = 0
        while ( $i < $procs ) 
           echo $split[1] >> ${FTMP}
           @ i++
        end
    else #1, PBS type
        echo $mach >> ${FTMP}
    endif
end
echo "End the list of processes..."

set proclist=`cat ${FTMP}`

set nproc=$#proclist
echo "proclist:" $proclist
echo "number of processors: $nproc"

set mpi0 = 0
set mpi = 1
echo "MPI for lapw0= $mpi0"
echo "MPI for lapw1/2= $mpi"

if ( $nproc > 1 ) then 
    echo "create ${FWMACH} file"
    echo '#' > ${FWMACH}

    if ( $mpi0 > 0 ) then 
      echo -n 'lapw0:' >> ${FWMACH}
      set i=1
      while ($i <= $nproc )
        echo -n "$proclist[$i] " >>${FWMACH}
        @ i = $i + $mpi0
      end
      echo ' ' >>${FWMACH}
    endif

#example for k-point and mpi parallel lapw1/2
    set i=1
    while ($i <= $nproc )
      echo -n '1:' >>${FWMACH}
      @ i1 = $i + $mpi
      @ i2 = $i1 - 1
      echo $proclist[$i-$i2] >>${FWMACH}
      set i=$i1
    end
    echo '' >>${FWMACH}
    echo 'granularity:1' >>${FWMACH}
    echo 'extrafine:1' >>${FWMACH}
#echo 'lapw2_vector_split:1 ' >>${FWMACH}


